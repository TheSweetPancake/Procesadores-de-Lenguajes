(* ==================== Símbolo inicial ==================== *)
cvs             ::= {global_var} cv {cv} ;

(* ==================== Variables ==================== *)
gloabal_var      ::= GVAR LL_A variable {variable} LL_C ;
local_var        ::= LVAR LL_A variable {variable} LL_C ;
variable         ::= IDENT IG CONJPALYNUM PYC ;

(* ==================== Curriculvbum Vitae ==================== *)
cv               ::= CV IDENT LL_A [local_var] datospersonales formacion [idiomas] [experiencia] [habilidades] [portafolio] LL_C ;

(* ==================== Datos personales ==================== *)
datospersonales  ::= DP LL_A nomyape [foto] fecha [bio] contacto LL_C ;
nomyape          ::= NOMYAPE PA_A CONJPALYNUM PA_C
                    |NOMYAPE PA_A IDENT PA_C;
foto             ::= FOTO PA_A RUTA PA_C ;
fecha            ::= FECHA PA_A FECHA_NUM PA_C ;
bio              ::= BIO PA_A CONJPALYNUM PA_C
                    |BIO PA_A IDENT PA_C;
contacto         ::= CONTACTO LL_A email telefono redes LL_C ;
email            ::= EMAIL PA_A MAIL PA_C ;
telefono         ::= TELEFONO PA_A TFNO PA_C ;
redes            ::= REDES LL_A linkedin [github] [web] LL_C
                    |REDES LL_A github [web] LL_C
                    |REDES LL_A web LL_C;
linkedin         ::= LINKEDIN PA_A RUTA PA_C ;
github           ::= GITHUB PA_A RUTA PA_C ;
web              ::= WEB PA_A RUTA PA_C ;

(* ==================== Formación ==================== *)
formacion        ::= FORMACION LL_A oficial {oficial} {complementaria} LL_C ;
oficial          ::= OFICIAL LL_A titulo expedidor [descripcion] [logros] fecha LL_C ;
titulo           ::= TITULO PA_A CONJPALYNUM PA_C
                    |TITULO PA_A IDENT PA_C;
expedidor        ::= EXPEDIDOR PA_A CONJPALYNUM PA_C
                    |EXPEDIDOR PA_A IDENT PA_C;
descripcion      ::= DESCRIPCION PA_A CONJPALYNUM PA_C
                    |DESCRIPCION PA_A IDENT PA_C;
logros           ::= LOGROS PA_A CONJPALYNUM PA_C
                    |LOGROS PA_A IDENT PA_C;
complementaria   ::= COMPLEMENTARIA LL_A titulo [certificado] expedidor [horas] fecha LL_C;
certificado      ::= CERTIFICADO PA_A BOOL PA_C;
horas            ::= HORAS PA_A NUM PA_C; 

(* ==================== Idiomas ==================== *)
idiomas          ::= IDIOMAS LL_A idioma {idioma} LL_C ;
idioma           ::= IDIOMA LL_A nombre nvi [expedidor] LL_C  ;
nvi              ::= NIVEL PA_A NVI PA_C ;

(* ==================== Experiencia ==================== *)
experiencia      ::= EXPERIENCIA LL_A laboral {laboral} {voluntariado} LL_C
                    |EXPERIENCIA LL_A voluntariado {voluntariado} LL_C;
laboral          ::= LABORAL LL_A puesto horas organizacion [responsabilidades] LL_C;
puesto           ::= PUESTO PA_A CONJPALYNUM PA_C
                    |PUESTO PA_A IDENT PA_C;
responsabilidades::= RESPONSABILIDADES PA_A CONJPALYNUM PA_C
                    |RESPONSABILIDADES PA_A IDENT PA_C;
voluntariado     ::= VOLUNTARIADO LL_A puesto descripcion horas organizacion LL_C;
organizacion     ::= ORGANIZACION PA_A CONJPALYNUM PA_C
                    |ORGANIZACION PA_A IDENT PA_C;

(* ==================== Habilidades ==================== *)
habilidades      ::= HABILIDADES LL_A soft [hard] LL_C
                    |HABILIDADES LL_A hard LL_C ;
soft             ::= SOFT LL_A habilidad {CO habilidad} LL_C;
hard             ::= HARD LL_A categoria {categoria} LL_C;
nvh              ::= NVHAB PA_A NVH PA_C;
habilidad        ::= HABILIDAD PA_A CONJPALYNUM PA_C
                    |HABILIDAD PA_A IDENT PA_C;
categoria        ::= CATEGORIA LL_A nombre habilidad nvh {CO habilidad nvh} LL_C;

(* ==================== Portafolio ==================== *)
portafolio       ::= PORTAFOLIO LL_A proyecto {proyecto} {merito} LL_C
                    |PORTAFOLIO LL_A merito {merito} LL_C;
proyecto         ::= PROYECTO LL_A nombre [grupo] descripcion tecnologias [web] LL_C;
nombre           ::= NOMBRE PA_A CONJPALYNUM PA_C
                    |NOMBRE PA_A IDENT PA_C;
grupo            ::= GRUPO LL_A companero {companero} LL_C;
companero        ::= COMPANERO LL_A nomyape [github] LL_C;
tecnologias      ::= TECNOLOGIAS PA_A CONJPALYNUM PA_C
                    |TECNOLOGIAS PA_A IDENT PA_C;
merito           ::= MERITOS LL_A nombre descripcion LL_C;
